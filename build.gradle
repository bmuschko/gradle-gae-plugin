apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
group = 'org.gradle.api.plugins'
version = '0.7.7'

repositories {
    mavenCentral()
}

dependencies {
    groovy fileTree(dir: new File(gradle.gradleHomeDir, 'lib'), includes: ['**/groovy-all-*.jar'])
    compile gradleApi()
    compile 'eu.appsatori:gradle-fatjar-plugin:0.1.3', {
        ext.optional = true
    }
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle GAE plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0'
}

task sourcesJar(type: Jar) {
    description 'An archive of the source code for Maven Central'
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    description 'An archive of the GroovyDocs for Maven Central'
    classifier 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

ext.pomConfiguration = {
    name 'Gradle GAE plugin'
    description 'Gradle plugin that provides tasks for uploading, running and managing Google App Engine projects.'
    url 'https://github.com/bmuschko/gradle-gae-plugin'
    inceptionYear '2011'

    scm {
        url 'https://github.com/bmuschko/gradle-gae-plugin'
        connection 'scm:https://bmuschko@github.com/bmuschko/gradle-gae-plugin.git'
        developerConnection 'scm:git://github.com/bmuschko/gradle-gae-plugin.git'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'bmuschko'
            name 'Benjamin Muschko'
            email 'benjamin.muschko@gmail.com'
        }
        developer {
            id 'erdi'
            name 'Marcin Erdmann'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project pomConfiguration
    }
}

if(hasProperty('mavenCentralUsername') && hasProperty('mavenCentralPassword')) {
    signing {
        required { gradle.taskGraph.hasTask(uploadArchives) && !version.endsWith('SNAPSHOT') }
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { deployment ->
                signing.signPom(deployment)
            }

            name = 'mavenCentralReleaseDeployer'

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
                releases(updatePolicy: 'always')
            }

            pom.project pomConfiguration
        }
    }
}
